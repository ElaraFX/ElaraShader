#include "stdosl.h"
#include "texture3d.h"
#include "mayautil.h"

shader maya_snow
[[ string help = "Maya snow"]]
(
                    /* "marble" parameters */
                    color i_snowColor = 1,
                    color i_surfaceColor = color(0.5, 0, 0),
                    float i_threshold = 0.5,
                    float i_depthDecay = 5.0,
                    float i_thickness = 1.0,

                    /* "Color Balance" related parameters */
                    color i_defaultColor = 0.5, 
                    color i_colorGain = 1.0, 
                    color i_colorOffset = 0.0,
                    float i_alphaGain = 1.0, 
                    float i_alphaOffset = 0.0,
                    /*uniform*/ int i_alphaIsLuminance = 0,

                    /* "Effects" related parameters */
                    /*uniform*/ int i_invert = 0,
                    int i_local = 0, 
                    int i_wrap = 1, 
                    int i_blend = 0,

                    /* geomtric inputs */
                    normal i_normalCamera = normal(0, 0, 1),
                    matrix i_placementMatrix = matrix(   1.0, 0.0, 0.0, 0.0,
                                                        0.0, 1.0, 0.0, 0.0,
                                                        0.0, 0.0, 1.0, 0.0,
                                                        0.0, 0.0, 0.0, 1.0),

	/* outputs */
	output color o_outColor = 1.0,
	output float o_outAlpha = 1.0
)
{
	/* extern point P;
	extern float du, dv; */
	
	float outside;
	float edgeDist;

	point pp = transformP(i_blend, 
		i_local, 
		i_placementMatrix, 
		i_wrap, edgeDist, 
		outside );

	if( outside < 1 )
	{
        //normal 
        normal normalCamera = normalize(N);
		normal texNormal;
		if( i_local != 0.0 )
			texNormal = transform("object", normalCamera);
		else
			texNormal = transform("world", normalCamera);

		texNormal = transform( i_placementMatrix, texNormal );
        
		float cosangle = dot(normalize(texNormal), vector(0, 1, 0));//compare Y-axis
		if( cosangle < 0 )
		{
			/* In Maya's world, snow cannot stick under objects */
			o_outColor = i_surfaceColor;
		}
		else
		{
			if( cosangle > i_threshold )
			{
				color snow = mix(i_snowColor, i_surfaceColor, 1-i_thickness );
				float mixValue = (1 - cosangle) / (1-i_threshold);
				mixValue = pow( mixValue, i_depthDecay );
				o_outColor = mix(snow, i_surfaceColor, mixValue);
			}
			else
			{
				o_outColor = i_surfaceColor;
			}
		}

		o_outAlpha = 1;
	}
	else
	{
		o_outColor = i_defaultColor;
		o_outAlpha = 0;
	}
}


